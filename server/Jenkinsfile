pipeline {
    agent {
        kubernetes {
            label 'kaniko-git-mongo-node'
            defaultContainer 'kaniko'
        }
    }

    stages {
        stage("Test") {
            steps {
                script {
                    container("node") {
                        dir("server") {
                            sh "npm install"
                            sh "npm test -- --reporter json > mocha.json || true"
                        }
                        def results = readJSON file: 'server/mocha.json'
                        if (results == null || results.stats == null) {
                            currentBuild.result = 'UNSTABLE'
                            error('Test results not found or invalid')
                        }
                        int tests = results.stats.tests as int
                        int passes = results.stats.passes as int
                        int failures = results.stats.failures as int
                        if (failures > 0 || passes < tests) {
                            unstable("Tests not all passing: ${passes}/${tests}, failures: ${failures}")
                            error('Failing pipeline due to test failures')
                        }
                    }
                }
            }
        }
        stage('Build and Push') {
            steps {
                script {
                    container('kaniko') {
                        String tag = null
                        dir('todo-app') {
                            String valuesFile = 'server-values.yaml'
                            def yaml = readYaml file: valuesFile
                            tag = yaml.image.tag
                        }
                        if (tag != null) {
                            sh """
                                /kaniko/executor \
                                --context=./server \
                                --dockerfile=./server/Dockerfile \
                                --destination=volk22kaimn/todo-app-server:${tag}
                            """
                            currentBuild.displayName = "Build #${env.BUILD_NUMBER} - tag: ${tag}"
                        }
                    }
                }
            }
        }
        stage("Deploy") {
            when {
                branch 'main'
            }
            steps {
                build job: 'CD', parameters: [string(name: 'APP_TO_DEPLOY', value: 'server')]
            }
        }
    }
    post {
        success {
            mail to: 'zeevdev22@gmail.com',
                subject: "[SUCCESS] ${env.JOB_NAME} #${env.BUILD_NUMBER}",
                body: "Build OK: ${env.BUILD_URL}"
        }
        failure {
            mail to: 'zeevdev22@gmail.com',
                subject: "[FAILURE] ${env.JOB_NAME} #${env.BUILD_NUMBER}",
                body: "Failed: ${env.BUILD_URL}"
        }
    }
}